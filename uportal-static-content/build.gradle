plugins {
  id "java"
  id "maven"
  id "com.diffplug.gradle.spotless" version "3.24.2"
  id "org.springframework.boot" version "2.4.2"
  id "io.spring.dependency-management" version "1.0.6.RELEASE"
  id "net.linguica.maven-settings" version "0.5"

  id "pmd"
  id "org.owasp.dependencycheck" version "5.3.0"
}

bootJar {
  launchScript()
  archiveBaseName = 'uportal-static-content'
}

group 'oakland.integration'
version = '1.0.0.RC'
description = "Web App for static content in uPortal portlets/soffits"

repositories {
  mavenLocal()
  mavenCentral()
  maven {url 'https://repo.gradle.org/gradle/libs-releases'}
}

if (project.hasProperty('local')) {
  apply from: rootProject.file('gradle/profile_local.gradle');
  println 'Using profile: "local"'
} else {
  println 'Using profile: "default"'
}

tasks.withType(Test) {
  testLogging {
    exceptionFormat "full"
    events "skipped", "passed", "failed"
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:unchecked'
  options.deprecation = true;
}

spotless {
  java {
    googleJavaFormat()
    importOrder 'edu', 'java', 'com org', ''
    removeUnusedImports()
    trimTrailingWhitespace()
  }
}

/* allows stdout and stderr to be displayed when running tests during build */
test {
  testLogging.showStandardStreams = true
  useJUnitPlatform()
}

sourceCompatibility = '8'
targetCompatibility = '8'

pmd {
  toolVersion = "6.21.0"
  ignoreFailures = "true"
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

  testCompile 'org.springframework.boot:spring-boot-starter-test'
}

clean.finalizedBy("spotlessApply")
